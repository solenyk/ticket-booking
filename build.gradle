plugins {
	id 'org.springframework.boot' version '3.4.4' apply false
	id 'io.spring.dependency-management' version '1.1.4' apply true
	id 'com.diffplug.spotless' version '7.0.3' apply true
}

allprojects {
	repositories {
		mavenCentral()
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.3"
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.diffplug.spotless'

	group = 'io.kopchak.ticket.booking'

	java {
		sourceCompatibility = JavaVersion.VERSION_21
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		// Web
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

		// Lombok
		compileOnly 'org.projectlombok:lombok:1.18.30'
		annotationProcessor 'org.projectlombok:lombok:1.18.30'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}

	// Code Formatter
	spotless {
		java {
			palantirJavaFormat()
			removeUnusedImports()
			licenseHeaderFile(rootProject.file("license-header.txt"))
			importOrder('java', 'javax|jakarta', '', 'org.springframework', 'kopchak.microservices', '\\#')
		}
	}

	compileJava.dependsOn 'spotlessApply'
}
